% simple_english.pl - File for HW05, T01
% A simple DCG for a subset of the English language
%
% Author: T. Wilgenbusch, K. Hipkin, T. Fairman, B. Dell


% Simple English grammar
% (thanks to Manuel Ebert)
%----------------------------------------------------
% 1 (15 points): Write a DCG that generates and analyses
% all meaningful sentences using the following words:
%    sharks, goldfishes, hawks, finches,
%    boys,
%    eat, swim, fly, drown.
% "Meaningful" means that
%    - only birds can fly
%    - herbivores don't eat other beings,
%    - fishes and mammals can swim (but not birds)
%    - mammals and birds can drown (but not fishes)
% i.e. 'sharks fly' should not be generated by your grammar.
% Similarly, finches are not carnivores.
%
% Hints:
% - Classify the words and use your classes as node labels.
% - Don't bother with Chomsky Normal Form or other fancy things.
% - Mind that drowning and eating can, but not necessarily
%   do, take an object.
% - Use the symbol s as the start symbol of the grammar.
% - There should be 43 sentences generated by your grammar.
%----------------------------------------------------
%

% Only birds can fly.
s --> birds,[fly].

% Herbivores don't eat other beings.
% Mind that "eating" can, but not necessarily does, take an object.
s --> beings,[eat].
s --> carnivores,[eat],beings.

% Fishes and mammals can swim (but not birds).
s --> fishes,[swim].
s --> mammals,[swim].

% Mammals and birds can drown (but not fishes).
% Mind that "drowning" can, but not necessarily does, take an object.
% The object (not the subject) of "drowning" is the being that drowns.
s --> mammals,[drown].
s --> birds,[drown].
s --> beings,[drown],mammals.
s --> beings,[drown],birds.

% All beings are either herbivores or carnivores.
% (Additionally, all beings are either birds, fishes, or mammals.)
% (Only one of these facts is needed to represent all possible beings.)
beings --> herbivores.
beings --> carnivores.

% Sharks, hawks, and boys are carnivores.
carnivores --> [sharks].
carnivores --> [hawks].
carnivores --> [boys].

% Goldfishes and finches are herbivores.
herbivores --> [goldfishes].
herbivores --> [finches].

% Hawks and finches are birds.
birds --> [hawks].
birds --> [finches].

% Sharks and goldfishes are fishes.
fishes --> [sharks]. % verified by google.com
fishes --> [goldfishes].

% Boys are mammals.
mammals --> [boys].

%----------------------------------------------------
% 2 (5 points). On an ancient scroll written in an even more
% ancient language called 'Pthu', the following sentences
% were found:
%
%    Nqblubh Mhboa.
%    Nqrrroah Ukahama Nqblubh. 
%    Nqlhalha Falup.
%    Nqlhalha Ukahama. 
%    Nqlhalha Ukahama Nqflua. 
%    Nqflua Sharabrab. 
%    Nqblubh Falup Nqdwingi.
%    Nqflua Ukahama Nqdwingi.
%    Nqdwingi Falup Nqflua.
%
% Assume the syntactic structure of Pthu is the same
% as it is in English.
% Use the grammar from the previous task to give an English
% translation for every word of the scroll.
%
% Hints: 
% - Instantiate every Pthu word with an English word and
%   check if all the so-translated sentences are meaningful.
% - You may use built-in predicates for list processing.
% - If you don't get an unique solution or if you get no solution
%   at all, it might be because your grammar generates
%   a set of sentences different than the intended one.
%   There should be 43 sentences generated by your grammar.
%----------------------------------------------------
%

%%
% unique(+List) - predicate that verifies that all elements in
% the given list are unique; intended for use with flat lists,
% but this expectation is not a requirement
%
% +List - the list
%%
unique([]).
unique([X|R]) :-
    maplist(dif(X), R),
    unique(R).

%%
% translation(...) - predicate that takes all of the Pthu words and
% attempts to instantiate them with English words such that all
% of the Pthu sentences translate to meaningful English sentences
%
% ... - the Pthu words to be translated
%%
translation(Nqrrroah, Nqblubh, Nqflua, Nqdwingi,
            Nqlhalha, Ukahama, Mhboa, Sharabrab, Falup) :-
    s([Nqblubh,Mhboa], []),
    s([Nqrrroah,Ukahama,Nqblubh], []),
    s([Nqlhalha,Falup], []),
    s([Nqlhalha,Ukahama], []),
    s([Nqlhalha,Ukahama,Nqflua], []),
    s([Nqflua,Sharabrab], []),
    s([Nqblubh,Falup,Nqdwingi], []),
    s([Nqflua,Ukahama,Nqdwingi], []),
    s([Nqdwingi,Falup,Nqflua], []),
    unique([Nqrrroah, Nqblubh, Nqflua, Nqdwingi,
            Nqlhalha, Ukahama, Mhboa, Sharabrab, Falup]).

% Translation results:
%   ? translation(Nqrrroah, Nqblubh, Nqflua, Nqdwingi,
%                 Nqlhalha, Ukahama, Mhboa, Sharabrab, Falup).
% 	Falup = drown,
% 	Mhboa = swim,
% 	Nqblubh = goldfishes,
% 	Nqdwingi = finches,
% 	Nqflua = hawks,
% 	Nqlhalha = boys,
% 	Nqrrroah = sharks,
% 	Sharabrab = fly,
% 	Ukahama = eat
% 	false
